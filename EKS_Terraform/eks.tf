resource "aws_security_group" "fleury_cluster_sg" {
  vpc_id = aws_vpc.fleury_vpc.id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "fleury-cluster-sg"
  }
}

resource "aws_security_group" "fleury_node_sg" {
  vpc_id = aws_vpc.fleury_vpc.id

  ingress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "fleury-node-sg"
  }
}

resource "aws_eks_cluster" "fleury" {
  name     = "fleury-cluster"
  role_arn = aws_iam_role.fleury_cluster_role.arn

  vpc_config {
    subnet_ids         = aws_subnet.fleury_public_subnet[*].id
    security_group_ids = [aws_security_group.fleury_cluster_sg.id]
  }
}

resource "aws_eks_node_group" "fleury" {
  cluster_name    = aws_eks_cluster.fleury.name
  node_group_name = "fleury-node-group"
  node_role_arn   = aws_iam_role.fleury_node_group_role.arn
  subnet_ids      = aws_subnet.fleury_public_subnet[*].id

  scaling_config {
    desired_size = 3
    max_size     = 3
    min_size     = 3
  }

  instance_types = ["t2.large"]

  remote_access {
    ec2_ssh_key               = var.ssh_key_name
    source_security_group_ids = [aws_security_group.fleury_node_sg.id]
  }
}

resource "aws_iam_role" "fleury_cluster_role" {
  name = "fleury-cluster-role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Service": "eks.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF
}

resource "aws_iam_role_policy_attachment" "fleury_cluster_role_policy" {
  role       = aws_iam_role.fleury_cluster_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKSClusterPolicy"
}

resource "aws_iam_role" "fleury_node_group_role" {
  name = "fleury-node-group-role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF
}

resource "aws_iam_role_policy_attachment" "fleury_node_group_role_policy" {
  role       = aws_iam_role.fleury_node_group_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKSWorkerNodePolicy"
}

resource "aws_iam_role_policy_attachment" "fleury_node_group_cni_policy" {
  role       = aws_iam_role.fleury_node_group_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEKS_CNI_Policy"
}

resource "aws_iam_role_policy_attachment" "fleury_node_group_registry_policy" {
  role       = aws_iam_role.fleury_node_group_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
}
